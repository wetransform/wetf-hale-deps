buildscript {
    repositories {
		mavenLocal()
		mavenCentral()
    }
    dependencies {
        classpath 'org.standardout:bnd-platform:0.3-SNAPSHOT' // bug in 0.2
    }
}

apply plugin: 'platform'

defaultTasks 'clean', 'updateSiteZip'

task wrapper(type: Wrapper) {
	gradleVersion = '1.11'
}

repositories {
	mavenCentral()
	maven {
		url 'http://oss.sonatype.org/content/repositories/snapshots/'
	}
}

ext {
	scalaVersion = '2.10'
}

configurations {
	platform {
		// exclude Scala (provided through other means)
		exclude group: 'org.scala-lang', module: 'scala-library'
	}
}

dependencies {
	// secure social
	//TODO switch to 2.1.4 once it is out
	platform group: "ws.securesocial", name: "securesocial_$scalaVersion", version: "master-SNAPSHOT", {
		// exclude anything play related (we only depend on model classes)
		exclude group: 'com.typesafe.play'
		exclude group: 'com.typesafe'
	}
	
	// slick 2
	platform group: "com.typesafe.slick", name: "slick_$scalaVersion", version: "2.0.1"
	
	// H2 database for tests
	platform group: 'com.h2database', name: 'h2', version: '1.3.175'
	
	// needed in addition to joda-time as otherwise Scala will complain about missing constructors (e.g. in DateTime)
	platform 'org.joda:joda-convert:1.4'
	
	// data sources
	platform 'commons-dbcp:commons-dbcp:1.4'
}

platform {
	bnd group: "ws.securesocial", name: "securesocial_$scalaVersion", {
		// exclude anything play related (we only depend on model classes)
		instruction 'Export-Package', "securesocial.core.*;version=$version"
	}
	
	bnd group: "com.typesafe.slick", name: "slick_$scalaVersion", {
		// slick must be able to access the database drivers (uses Class.forName)
		
		// add optional dependency for h2 database
		//TODO postgres
		instruction 'Require-Bundle', 'org.h2;resolution:=optional'
	}
	
	bnd 'commons-dbcp:commons-dbcp', {
		// dbcp must be able to access database drivers
		
		// add optional dependency for h2 database
		//TODO postgres
		instruction 'Require-Bundle', 'org.h2;resolution:=optional'
	}
}
