buildscript {
    repositories {
		mavenLocal()
		mavenCentral()
		maven {
			url 'http://oss.sonatype.org/content/repositories/snapshots/'
		}
    }
    dependencies {
        classpath 'org.standardout:bnd-platform:1.1-SNAPSHOT'
	classpath 'org.standardout:gradle-versioneye-plugin:0.2-SNAPSHOT'
    }
}

apply plugin: 'platform'
apply plugin: 'versioneye'

defaultTasks 'clean', 'updateSiteZip'

task wrapper(type: Wrapper) {
	gradleVersion = '1.11'
}

repositories {
	mavenCentral()
	maven {
		url 'http://oss.sonatype.org/content/repositories/snapshots/'
	}
	maven {
		url 'http://repo.typesafe.com/typesafe/maven-releases'
	}
}

ext {
	scalaVersion = '2.10'
	playVersion = '2.2.3'
	jgitVersion = '3.2.0.201312181205-r'
}

configurations {
	platform {
		// exclude Scala (provided through other means)
		exclude group: 'org.scala-lang', module: 'scala-library'
		// exclude slf4-api (causes conflig (wtf!) with newer version in HALE platform)
		exclude group: 'org.slf4j', module: 'slf4j-api'
	}
}

dependencies {
	// secure social
	platform group: "ws.securesocial", name: "securesocial_$scalaVersion", version: "2.1.x-SNAPSHOT", {
		// exclude anything play related (we only depend on model classes)
		exclude group: 'com.typesafe.play'
		exclude group: 'com.typesafe'
	}

	// play Json API
	platform group: "com.typesafe.play", name: "play-json_$scalaVersion", version: playVersion
	
	// slick 2
	platform group: "com.typesafe.slick", name: "slick_$scalaVersion", version: "2.0.1"
	
	// H2 database for tests
	platform group: 'com.h2database', name: 'h2', version: '1.3.175'
	
	// needed in addition to joda-time as otherwise Scala will complain about missing constructors (e.g. in DateTime)
	platform 'org.joda:joda-convert:1.4'
	
	// data sources
	platform 'commons-dbcp:commons-dbcp:1.4'
	
	// staxon
	platform 'de.odysseus.staxon:staxon:1.3'
	
	// JGit
	platform group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: jgitVersion 
	platform group: 'org.eclipse.jgit', name: 'org.eclipse.jgit.java7', version: jgitVersion
}

platform {
	// generate package imports w/ version numbers
	determineImportVersions = true
	
	bnd group: "ws.securesocial", name: "securesocial_$scalaVersion", {
		// exclude anything play related (we only depend on model classes)
		instruction 'Export-Package', "securesocial.core.*;version=$version"
		// make play related imports optional
		optionalImport 'play.*', 'com.typesafe.plugin', 'views.*'
		//XXX specify version for SNAPSHOT - remove when switching to release
		version = '2.1.4.SNAPSHOT'
	}
	
	bnd group: 'com.h2database', name: 'h2', {
		optionalImport 'org.osgi.service.jdbc'
	}

	// JDBC drivers	
	def jdbcBundles = ['org.h2', 'org.postgresql.jdbc']
	def requireJdbc = jdbcBundles.collect{ it + ';resolution:=optional' }.join(',')
	
	bnd group: "com.typesafe.slick", name: "slick_$scalaVersion", {
		// slick must be able to access the database drivers (uses Class.forName)
		instruction 'Require-Bundle', requireJdbc
	}
	
	bnd 'commons-dbcp:commons-dbcp', {
		// dbcp must be able to access database drivers
		instruction 'Require-Bundle', requireJdbc
	}
}
